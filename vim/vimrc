" This must be first, because it changes other options as side effect
set nocompatible

filetype off


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Plugins
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

set rtp+=~/.vim/bundle/Vundle.vim
call vundle#begin()

" Vundle
Plugin 'gmarik/Vundle.vim'              " Vim plugin manager

" Tools
Plugin 'chrisbra/SudoEdit.vim'          " Edit/Write via sudo
Plugin 'reedes/vim-pencil'              " Vim as a tool writing

" User interface
Plugin 'bling/vim-airline'              " Custom status bar
Plugin 'kien/ctrlp.vim'                 " Fuzzy search
Plugin 'mileszs/ack.vim'                " Search in directory
Plugin 'morhetz/gruvbox'                " Color scheme
Plugin 'vim-airline/vim-airline-themes' " Themes for status bar

" Fast coding
Plugin 'airblade/vim-gitgutter'         " Git gutter
Plugin 'ervandew/supertab'              " Code completion
Plugin 'lightcode/PasteMode.vim'        " Improved paste mode
Plugin 'scrooloose/syntastic'           " Code checker

" Language specific plugins
Plugin 'pearofducks/ansible-vim'        " Ansible
Plugin 'vim-scripts/bats.vim'           " Bats
Plugin 'jjo/vim-cue'                    " Cuelang
Plugin 'ekalinin/Dockerfile.vim'        " Dockerfile
Plugin 'fatih/vim-go'                   " Go
Plugin 'jelera/vim-javascript-syntax'   " JavaScript
Plugin 'lepture/vim-jinja'              " Jinja
Plugin 'google/vim-jsonnet'             " Jsonnet
Plugin 'tpope/vim-markdown'             " Markdown
Plugin 'LnL7/vim-nix'                   " Nix
Plugin 'hashivim/vim-terraform'         " Terraform
Plugin 'cespare/vim-toml'               " Toml
Plugin 'posva/vim-vue'                  " Vue.js

call vundle#end()

filetype plugin indent on


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Plugins Configuration
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

"""
" Plugin 'mileszs/ack.vim'
"""

if executable('ag')
  let g:ackprg = 'ag --vimgrep'
endif


"""
" Plugin 'tpope/vim-markdown'
"""

let g:markdown_fenced_languages = ['yaml', 'xml', 'json']
let g:markdown_syntax_conceal = 0


"""
" Plugin 'reedes/vim-pencil'
"""

let g:pencil#autoformat = 0
let g:pencil#conceallevel = 0
let g:pencil#wrapModeDefault = 'soft'


"""
" Plugin 'fatih/vim-go'
"""

let g:go_highlight_functions = 1
let g:go_highlight_methods = 1
let g:go_highlight_structs = 1
let g:go_highlight_operators = 1
let g:go_highlight_build_constraints = 1

if executable('goimports')
    let g:go_fmt_command = 'goimports'
endif


"""
" Plugin 'kien/ctrlp.vim'
"""

let g:ctrlp_match_window = 'bottom,order:btt,min:1,max:12,results:12'

" Setup some default ignores
let g:ctrlp_custom_ignore = {
    \ 'dir':  '\v[\/](\.(git|hg|svn))$',
    \ 'file': '\v\.(exe|so|dll|class|png|jpg|jpeg)$' }

" If available, use ag instead of grep to do research
if executable('ag')
    " Use Ag over Grep
    set grepprg=ag\ --nogroup\ --nocolor

    " Use ag in CtrlP for listing files. Lightning fast and respects .gitignore
    let g:ctrlp_user_command = 'ag %s -l --hidden --ignore .git --nocolor -g ""'
endif


"""
" Plugin 'bling/vim-airline'
"""

set noshowmode

let g:airline_theme = 'bubblegum'
let g:airline_powerline_fonts = 0
let g:airline_skip_empty_sections = 1
let g:airline_left_sep = ''
let g:airline_left_alt_sep = ''
let g:airline_right_sep = ''
let g:airline_right_alt_sep = ''
let g:airline_branch_prefix = ''
let g:airline_linecolumn_prefix = ''

let g:airline_extensions = ['tabline', 'ctrlp', 'syntastic', 'whitespace']
let g:airline#extensions#tabline#enabled = 1
let g:airline#extensions#tabline#fnamemod = ':t'
let g:airline#extensions#tabline#left_sep = ''
let g:airline#extensions#tabline#left_alt_sep = ''
let g:airline#extensions#tabline#right_sep = ''
let g:airline#extensions#tabline#right_alt_sep = ''


"""
" Plugin 'scrooloose/syntastic'
"""

let g:syntastic_always_populate_loc_list = 1
let g:syntastic_auto_loc_list = 2
let g:syntastic_check_on_open = 1
let g:syntastic_check_on_wq = 1

" Here we can make check active or passive.
" I made `rst` checking passive because it doesn't
" handle Sphinx well.
let g:syntastic_mode_map = {
    \ 'mode': 'active',
    \ 'active_filetypes': [],
    \ 'passive_filetypes': ['rst'] }

let g:syntastic_error_symbol = '✗'
let g:syntastic_warning_symbol = '⚠'

let g:syntastic_python_checkers = ['flake8']
let g:syntastic_go_checkers = ['go', 'golint', 'govet']
let g:syntastic_javascript_checkers = ['jshint']
let g:syntastic_yaml_checkers = ['yamllint']
let g:syntastic_ansible_checkers = ['yaml/yamllint']


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Search
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" Set show matching parenthesis
set showmatch
" How many tenths of a second to blink when matching brackets
set mat=1
" Ignore case when searching
set ignorecase
" Ignore case if search pattern is all lowercase, case-sensitive otherwise
set smartcase
" Insert tabs on the start of a line according to shiftwidth, not tabstop
set smarttab
" Highlight search terms
set hlsearch
" Show search matches as you type
set incsearch


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Text, tab and indent related
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" Allow backspacing over everything in insert mode
set backspace=indent,eol,start
" Use spaces instead of tabs
set expandtab
" Always set autoindenting on
set autoindent
" Copy the previous indentation on autoindenting
set copyindent
" Use multiple of shiftwidth when indenting with '<' and '>'
set shiftround
" Don't wrap lines
set nowrap
" Number of spaces to use for autoindenting
set shiftwidth=4
" A tab is four spaces
set tabstop=4
" Enable indent
filetype indent on
" Disable auto-comments
autocmd BufNewFile,BufRead,FileType * setlocal formatoptions-=cro
" Show trailing spaces
set list
set listchars=trail:•,extends:#,nbsp:.,tab:\ \ ,


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Misc
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

set timeoutlen=1000
set ttimeoutlen=0
" Always show line numbers
set number
" This allows buffers to be hidden if you've modified a buffer.
set hidden
" Always show current position
set ruler
" Height of the command bar
set cmdheight=1
" Remember more commands and search history
set history=1000
" Use many muchos levels of undo
set undolevels=1000
" Don't beep
set visualbell
" Don't beep
set noerrorbells
" Show cmd
set showcmd
" Visual autocomplete for command menu
set wildmenu
set wildignore=*.swp,*.bak,*.pyc,*.class
" Don't redraw when don't have to
set lazyredraw
" Keep at least 5 lines above/below
set scrolloff=5
" Keep at least 5 lines left/right
set sidescrolloff=5
" We have a fast terminal
set ttyfast
" Disable mouse in terminal
if !has('gui_running')
    set mouse=
endif
" Disable blinking when we reach the first line
autocmd GUIEnter * set vb t_vb=
autocmd VimEnter * set vb t_vb=
" Highlight current line number
set cursorline


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Theme
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

set laststatus=2

" Enable syntax highlighting
if &t_Co >= 256 || has('gui_running')
    let g:rehash256 = 1
    silent! colorscheme gruvbox
endif

set background=dark

if &t_Co > 2 || has('gui_running')
    " Switch syntax highlighting on, when the terminal has colors
    syntax enable
endif

" Disable background color because it adds spaces in terminal
highlight Normal ctermbg=none

" Fix background color on SignColumn for GitGutter
highlight clear SignColumn
highlight GitGutterAdd ctermfg=green
highlight GitGutterChange ctermfg=yellow
highlight GitGutterDelete ctermfg=red
highlight GitGutterChangeDelete ctermfg=yellow


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Editing mappings
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" With a map leader it's possible to do extra key combinations
" like <leader>w saves the current file
let mapleader = ','

" Fast saving
nnoremap <silent> <leader>w :w!<cr>

" Hide highlight when <leader><cr> is pressed
nnoremap <silent> <leader><cr> :noh<cr>

" Close buffer
nnoremap <silent> <leader>q :GracefulQuit<cr>

" Fast moving between buffers
nnoremap <silent> <leader><Left> :bprevious<cr>
nnoremap <silent> <leader><Right> :bnext<cr>

" Toggle copy/paste mode
nnoremap <silent> <C-k><C-p> :PasteModeToggle<cr>
inoremap <C-k><C-p> <C-O>:PasteModeToggle<cr>

" Check the code with syntastic
nnoremap <silent> cc :SyntasticCheck<cr> :Errors<cr>
nnoremap <silent> cq :lclose<cr>
nnoremap <silent> cp :lprevious<cr>
nnoremap <silent> cn :lnext<cr>

" Strip trailing whitespace
nnoremap <silent> <leader>s :call StripTrailingWhitespaces()<cr>


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Language specific configuration
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

augroup LanguageConfiguration
    autocmd!

    autocmd FileType plaintex      setlocal filetype=tex

    autocmd FileType css           setlocal tabstop=2 shiftwidth=2 softtabstop=2
    autocmd FileType cue           setlocal noexpandtab tabstop=4 shiftwidth=4 listchars=tab:>.,
    autocmd FileType go            setlocal noexpandtab tabstop=4 shiftwidth=4
    autocmd FileType html,xml      setlocal tabstop=2 shiftwidth=2 softtabstop=2
    autocmd FileType java          setlocal noexpandtab formatprg=par\ -w80\ -T4
    autocmd FileType javascript    setlocal tabstop=2 shiftwidth=2 softtabstop=2
    autocmd FileType make          setlocal noexpandtab nolist
    autocmd FileType php           setlocal formatprg=par\ -w80\ -T4
    autocmd FileType python        setlocal colorcolumn=79 completeopt-=preview listchars=tab:>.,
    autocmd FileType ruby          setlocal tabstop=2 shiftwidth=2 softtabstop=2
    autocmd FileType sh,zsh        setlocal tabstop=2 shiftwidth=2 softtabstop=2
    autocmd FileType verilog       setlocal tabstop=2 shiftwidth=2 softtabstop=2
    autocmd FileType vue           setlocal tabstop=2 shiftwidth=2 softtabstop=2
    autocmd FileType yaml          setlocal tabstop=2 shiftwidth=2 softtabstop=2 commentstring=#\ %s
    autocmd FileType sql           setlocal tabstop=2 shiftwidth=2 softtabstop=2

    autocmd FileType markdown,mkd  call Prose() | call HugoMarkdown()
    autocmd FileType rst           call Prose() | setlocal tabstop=4 shiftwidth=4 softtabstop=4
    autocmd FileType text,tex      call Prose()
augroup END


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Functions
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

"""
" Set the current buffer in text mode
"""

function! Prose()
    call pencil#init()
    setlocal nocursorline
endfunction

command! -nargs=0 Prose call Prose()


"""
" Strip trailing whitespace in the current buffer
"""

function! StripTrailingWhitespaces()
    let l:winview = winsaveview()
    silent! %s/\s\+$//
    call winrestview(l:winview)
endfunction


"""
" Quit if their are no buffer open
"""

function! CountListedBuffers()
    let cnt = 0
    for nr in range(1,bufnr("$"))
        if buflisted(nr) && ! empty(bufname(nr))
            let cnt += 1
        endif
    endfor
    return cnt
endfunction

function! QuitIfNoBuffer(bang)
    if CountListedBuffers() <= 1
        if a:bang
            :quit!
        else
            :quit
        endif
    else
        if a:bang
            :bdelete!
        else
            :bdelete
        endif
    endif
endfunction

command -nargs=* -range -bang GracefulQuit call QuitIfNoBuffer(<bang>0)


"""
" Handle some specific highlight for Hugo
"""

function! HugoMarkdown()
    syn region hugoFrontmatter start='^+++$' end='^+++$'
    syn match hugoShortcode '{{.*}}'
    syn region hugoHighlight matchgroup=hugoShortcode start='{{<highlight.*>}}' end='{{</highlight>}}'

    hi link hugoShortcode Statement
    hi link hugoHighlight markdownCode
    hi link hugoFrontmatter Comment
endfunction
